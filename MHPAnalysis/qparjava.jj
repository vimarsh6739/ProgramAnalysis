options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(QParJavaParser)
  public class QParJavaParser {}
PARSER_END(QParJavaParser)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

TOKEN : 
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < COLON: ":" >
| < DOT: "." >
| < ASSIGN: "=" >
| < LT: "<" >
| < PLUS: "+" >
| < MINUS: "-" >
| < AND : "&&" >   
| < NOT : "!" >   
| < BOOLEAN: "boolean" >   
| < CLASS: "class" >
| < INTERFACE: "interface" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < SYNCH: "synchronized" >
| < FOR: "for" >
| < INTEGER: "int" >   
| < LENGTH: "length" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < PRINT: "System.out.println" >
| < VOID: "void" >         
| < SCOMMENT1: "/*">
| < SCOMMENT2: "*/">
| < MHP: "mhp?">
| < START: "start">
| < WAIT: "wait">
| < NOTIFY: "notify">
| < NOTIFYALL: "notifyAll">
| < JOIN: "join">
| < RUN: "run">
| < THREAD: "Thread">
| < TRY: "try">
| < CATCH: "catch">
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}


/************************************
 * The qparJava Grammar Starts Here *
 ************************************/


void Goal() :
{}
{
  MainClass()
  ( TypeDeclaration() )*
  (Query())*
  <EOF>
}

void MainClass() :
{}
{
  "class" Identifier() "{"
    "public" "static" "void" "main" "(" "String" "[" "]" Identifier() ")"   
    "{" 
      "try" "{" 
    ( LOOKAHEAD(VarDeclaration()) VarDeclaration() )* 
      ( QParStatement() )*
      "}" "catch" "(" Identifier() Identifier() ")" "{" "}" 
     "}"
  "}"
}

void TypeDeclaration() :  
{}
{ 
  LOOKAHEAD(3)
  ClassDeclaration()
|
  ClassExtendsDeclaration()
}

void ClassDeclaration() :
{}
{
  "class" Identifier() "{" ( VarDeclaration() )* 
                       "}"
}

void ClassExtendsDeclaration() :
{}
{
  "class" Identifier() "extends" "Thread"
                       "{" ( VarDeclaration() )* 
                           ( MethodDeclaration() )* 
                       "}"
}

void VarDeclaration() :  
{}
{ 
  Type() Identifier() ";"      
}

void MethodDeclaration() :  
{}
{
  "public" "void" "run" "("  ")"   
  "{" 
      "try" "{" 
      ( LOOKAHEAD(2) VarDeclaration() )* 
      ( QParStatement() )* 
      "}" "catch" "(" Identifier() Identifier() ")" "{""}" 
  "}"
}

void Type() :
{}
{
  BooleanType()
|
  IntegerType()
|
  Identifier()     
}

void BooleanType() :
{}
{
  "boolean"
}

void IntegerType() :
{}
{
  "int"
}
void QParStatement():
{}
{
	(Ann())*
	Statement()
}

void Ann():
{}
{
<SCOMMENT1>
 Label() 
<SCOMMENT2>
}

void Label():
{}
{
	Identifier() ":"  

}
void Statement() :
{}
{
  Block()
| 
  LOOKAHEAD(2)
  AssignmentStatement()
|
  LOOKAHEAD(FieldAssignmentStatement())
  FieldAssignmentStatement()
|
  IfStatement()
|
  WhileStatement()
|
  MessageSend()
| 
  PrintStatement()
|
  SynchStatement ()
}

void Block() :
{}
{
  "{" ( QParStatement() )* "}"
}

void AssignmentStatement() :
{}
{
  Identifier() "=" Expression() ";"
}

void FieldAssignmentStatement() :
{}
{
  Identifier() "." Identifier()  "=" Identifier() ";"
}

void IfStatement() :
{}
{
  "if" "(" Identifier() ")" Statement() "else" Statement() 
}

void WhileStatement() :
{}
{
  "while" "(" Identifier() ")" Statement() 
}
void SynchStatement() :
{}
{
  "synchronized" "(" Identifier() ")" Statement() 
}


void PrintStatement() :
{}
{
  "System.out.println" "(" Identifier() ")" ";"
}

void Expression() :
{}
{
  LOOKAHEAD( Identifier() "&&" )
  AndExpression()
|
  LOOKAHEAD( Identifier() "<" )
  CompareExpression()
|
  LOOKAHEAD( Identifier() "+" )
  PlusExpression()
|
  LOOKAHEAD( Identifier() "-" )
  MinusExpression()
|
  LOOKAHEAD( Identifier() "*" )
  TimesExpression()
|
  LOOKAHEAD( FieldRead() )
  FieldRead()
|
  PrimaryExpression()
}

void AndExpression() :
{} 
{
  Identifier() "&&" Identifier()
}

void CompareExpression() :
{} 
{
  Identifier() "<" Identifier()
}

void PlusExpression() :
{}
{
  Identifier() "+" Identifier()
}

void MinusExpression() :
{}
{
  Identifier() "-" Identifier()
}

void TimesExpression() :
{}
{
  Identifier() "*" Identifier()
}

void FieldRead() :
{}
{
  Identifier() "." Identifier()
}
void MessageSend() :
{}
{
  LOOKAHEAD(callStartMethod())
  callStartMethod() 
|
  LOOKAHEAD(callNotifyMethod())
  callNotifyMethod() 
|
  LOOKAHEAD(callNotifyAllMethod())
  callNotifyAllMethod() 
|
  LOOKAHEAD(callWaitMethod())
  callWaitMethod() 
|
  callJoinMethod() 
}
void callStartMethod() :
{}
{
  Identifier() "." "start" "(" ")" ";"
}

void callNotifyMethod() :
{}
{
  Identifier() "." "notify" "(" ")" ";"
}

void callNotifyAllMethod() :
{}
{
  Identifier() "." "notifyAll" "(" ")" ";"
}
void callWaitMethod() :
{}
{
  Identifier() "." "wait" "(" ")" ";"
}
void callJoinMethod() :
{}
{
  Identifier() "." "join" "(" ")" ";"
}

void PrimaryExpression() :
{}
{
  IntegerLiteral()
|
  TrueLiteral()
|
  FalseLiteral()
|
  Identifier()
|
  ThisExpression()
|
  AllocationExpression()
|   
  NotExpression()
}

void IntegerLiteral() :
{}
{
  <INTEGER_LITERAL>
}


void TrueLiteral() :
{}
{
  "true"
}

void FalseLiteral() :
{}
{
  "false"
}

void Identifier() :
{}
{
  <IDENTIFIER>
}

void ThisExpression() :
{}
{
  "this"
}

void AllocationExpression() :
{}
{
  "new" Identifier() "(" ")"
}

void NotExpression() :
{}
{
  "!" Identifier() 
}

void Query():
{}
{
<SCOMMENT1> Identifier() "mhp?" Identifier() <SCOMMENT2>

}
